:BUILD
SETLOCAL

IF NOT DEFINED ROOTDIR GOTO :EOF

IF NOT EXIST DOS3X\NUL MKDIR DOS3X
CD DOS3X

SET SRCDIR=%~dp0
SET SRCDIR=%SRCDIR:~0,-1%
SET INCDIR=%ROOTDIR%\lib\macros

REM CALL :BUILDONE 300
REM IF ERRORLEVEL 1 GOTO :EOF
CALL :BUILDONE 310
IF ERRORLEVEL 1 GOTO :EOF
CALL :BUILDONE 320
IF ERRORLEVEL 1 GOTO :EOF
REM CALL :BUILDONE 321
REM IF ERRORLEVEL 1 GOTO :EOF
CALL :BUILDONE 330
IF ERRORLEVEL 1 GOTO :EOF
CALL :BUILDONE 331
IF ERRORLEVEL 1 GOTO :EOF
CALL :BUILDONE 332
IF ERRORLEVEL 1 GOTO :EOF

ENDLOCAL
GOTO :EOF







:BUILDONE
SETLOCAL

SET VERS=%~1

MKDIR DOS%VERS%
CD DOS%VERS%

CALL :ASM reloc %VERS%
IF ERRORLEVEL 1 GOTO :EOF
CALL :ASM main %VERS%
IF ERRORLEVEL 1 GOTO :EOF
CALL :ASM filemgr %VERS%
IF ERRORLEVEL 1 GOTO :EOF
CALL :ASM boot1 %VERS%
IF ERRORLEVEL 1 GOTO :EOF
CALL :ASM boot2 %VERS%
IF ERRORLEVEL 1 GOTO :EOF
CALL :ASM rwts %VERS%
IF ERRORLEVEL 1 GOTO :EOF
CALL :ASM rwtsapi %VERS%
IF ERRORLEVEL 1 GOTO :EOF

%CC65DIR%\ld65 -v -C %SRCDIR%\dos.ld65config -m dos.map reloc.o65 main.o65 filemgr.o65 boot1.o65 boot2.o65 rwts.o65 rwtsapi.o65
IF ERRORLEVEL 1 GOTO :EOF

IF %VERS% GEQ 330 GOTO :DOS33

JAVA -cp %A2CDT% dd --skip=0x1B00 <dos >dos.d13
JAVA -cp %A2CDT% dd --count=0x1B00 <dos >>dos.d13
JAVA -cp %A2CDT% dd --count=0xB800 --const=0 >>dos.d13
JAVA -cp %A2CDT% CreateCatalog --version=%VERS% >>dos.d13
JAVA -cp %A2CDT% dd --count=0xDD00 --const=0 >>dos.d13

JAVA -cp %A2CDT% ConvertD13toNibble <dos.d13 >dos.nib

GOTO :DONEDISK



:DOS33
JAVA -cp %A2CDT% dd --skip=0x1B00 <dos >dos.do
JAVA -cp %A2CDT% dd --count=0x1B00 <dos >>dos.do
JAVA -cp %A2CDT% dd --count=0xEB00 --const=0 >>dos.do
JAVA -cp %A2CDT% CreateCatalog --version=%VERS% >>dos.do
JAVA -cp %A2CDT% dd --count=0x11000 --const=0 >>dos.do

JAVA -cp %A2CDT% ConvertD16toNibble <dos.do >dos.nib

GOTO :DONEDISK



:DONEDISK

ENDLOCAL
GOTO :EOF







:ASM
SETLOCAL

COPY /Y %SRCDIR%\%~1.s65 .

%CC65DIR%\ca65 -v -l -t apple2 -I %SRCDIR% -I %INCDIR% -o %~1.o65 -D VERSION=%~2 -D NODELAY %~1.s65
SET /A ERR=%ERRORLEVEL%

DEL /F /Q %~1.s65

IF %ERR% NEQ 0 EXIT /B %ERR%

ENDLOCAL
GOTO :EOF
