:BUILD
SETLOCAL

IF NOT DEFINED ROOTDIR GOTO:EOF

IF NOT EXIST DOS3X\NUL MKDIR DOS3X
CD DOS3X

SET SRCDIR=%~dp0
SET SRCDIR=%SRCDIR:~0,-1%
SET INCDIR=%rootdir%\lib\macros

CALL:BUILDONE 310
IF ERRORLEVEL 1 GOTO:EOF
CALL:BUILDONE 320
IF ERRORLEVEL 1 GOTO:EOF
CALL:BUILDONE 330
IF ERRORLEVEL 1 GOTO:EOF
CALL:BUILDONE 331
IF ERRORLEVEL 1 GOTO:EOF
CALL:BUILDONE 332
IF ERRORLEVEL 1 GOTO:EOF

ENDLOCAL
GOTO:EOF







:BUILDONE
SETLOCAL

SET VERS=%~1

MKDIR DOS%VERS%
CD DOS%VERS%

CALL:ASM RELOC %VERS%
IF ERRORLEVEL 1 GOTO:EOF
CALL:ASM MAIN %VERS%
IF ERRORLEVEL 1 GOTO:EOF
CALL:ASM FILEMGR %VERS%
IF ERRORLEVEL 1 GOTO:EOF
CALL:ASM BOOT1 %VERS%
IF ERRORLEVEL 1 GOTO:EOF
CALL:ASM BOOT2 %VERS%
IF ERRORLEVEL 1 GOTO:EOF
CALL:ASM RWTS %VERS%
IF ERRORLEVEL 1 GOTO:EOF
CALL:ASM RWTSAPI %VERS%
IF ERRORLEVEL 1 GOTO:EOF

%CC65DIR%\ld65 -v -C %SRCDIR%\dos.ld65config -m dos.map reloc.o65 main.o65 filemgr.o65 boot1.o65 boot2.o65 rwts.o65 rwtsapi.o65
IF ERRORLEVEL 1 GOTO:EOF

ENDLOCAL
GOTO:EOF







:ASM
SETLOCAL

COPY /Y %SRCDIR%\%~1.s65 .

%CC65DIR%\ca65 -v -l -t apple2 -I %SRCDIR% -I %INCDIR% -o %~1.o65 -D VERSION=%~2 %~1.s65
SET /A ERR=%ERRORLEVEL%

DEL /F /Q %~1.s65

IF %ERR% NEQ 0 EXIT /B %ERR%

ENDLOCAL
GOTO:EOF
