options
{
	IGNORE_CASE = true;
	STATIC = false;
}



PARSER_BEGIN(GedcomDateValueParser)

package nu.mine.mosher.gedcom.date.parser;

import java.util.HashMap;
import java.util.Map;

class GedcomDateValueParser
{
	private static final Map mapMonthTokenToInt = new HashMap();
	private static int nextMonth = 1;
	public static void addNextMonth(final String month)
	{
		mapMonthTokenToInt.put(month,new Integer(nextMonth));
		++nextMonth;
	}
}

PARSER_END(GedcomDateValueParser)



SKIP :
{
	" " | "\t" | "\n" | "\r"
}

TOKEN :
{
	< FROM:        "FROM" > |
	< TO:          "TO"   > |
	< BEFORE:      "BEF"  > |
	< AFTER:       "AFT"  > |
	< BETWEEN:     "BET"  > |
	< AND:         "AND"  > |
	< ABOUT:       "ABT"  > |
	< CALCULATED:  "CAL"  > |
	< ESTIMATED:   "EST"  > |
	< INTERPRETED: "INT"  >
}

TOKEN :
{
	< MONTH:
		"JAN" | "FEB" | "MAR" | "APR" | "MAY" | "JUN" |
		"JUL" | "AUG" | "SEP" | "OCT" | "NOV" | "DEC" >
		{
			GedcomDateValueParser.addNextMonth("JAN");
			GedcomDateValueParser.addNextMonth("FEB");
		}
}

TOKEN :
{
	< CALENDAR:  "GREGORIAN" | "JULIAN" >
}

TOKEN :
{
	< LPAREN:  "(" > |
	< RPAREN:  ")" > |
	< NUMBER:  (["0"-"9"])+ > |
	< SLASH:   "/" > |
	< AT:      "@" > |
	< RAWTEXT: ~[")"] >
}

TOKEN :
{
	< BC: "BC" | "B.C." > |
	< CALENDAR_ESC: "#D" >
}

void DateValue() : {}
{
	(
		DatePeriod() |
		DateRange() |
		DateApproximated() |
		DateInterpreted() |
		DatePhrase() |
		Date()
	)

	<EOF>
}

void DateRange() : {}
{
	<BEFORE> Date() | <AFTER> Date() | <BETWEEN> Date() <AND> Date()
}

void DateApproximated() : {}
{
	<ABOUT> Date() | <CALCULATED> Date() | <ESTIMATED> Date()
}

void DateInterpreted() : {}
{
	<INTERPRETED> Date() DatePhrase()
}

void DatePhrase() : {}
{
	<LPAREN> <RAWTEXT> <RPAREN>
}

void DatePeriod() : {}
{
	<FROM> Date() [ <TO> Date() ] |
	<TO> Date()
}

void Date() : {}
{
	[ DateCalendarEscape() ] DateCalendar()
}

void DateCalendarEscape() : {}
{
	<AT> <CALENDAR_ESC> <CALENDAR> <AT>
}

void DateCalendar() : {}
{
	[ LOOKAHEAD(2) [ Day() ] Month() ] Year()
}

int Day() : {}
{
	<NUMBER>
	{
		return Integer.parseInt(this.token.image);
	}
}

int Month() : {}
{
	<MONTH>
	{
		return 0;
	}
}

int Year() : {}
{
	{
		Token year;
		boolean gregorian = false;
		boolean bc = false;
	}
	year = <NUMBER>	[ gregorian = SlashYear() ]	[ LOOKAHEAD(2) bc = BC() ]
	{
		int y = Integer.parseInt(year.image);
		if (gregorian)
		{
			++y;
		}
		if (bc)
		{
			y = -y;
		}
		return y;
	}
}

boolean SlashYear() : {}
{
	<SLASH> <NUMBER>
	{
		return true;
	}
}

boolean BC() : {}
{
	[ <LPAREN> ] <BC> [ <RPAREN> ]
	{
		return true;
	}
}
