; RAMTEST
; BY WOZ
; 6/7
; COPYRIGHT 1978 BY:
; APPLE COMPUTER INC

; EQUATES:
TDATA           EQU $0                  ; TEST DATA $00 OR $FF
NDATA           EQU $1                  ; INVERSETESTDATA.
TESTD           EQU $2                  ; GALLOPDATA
R3L             EQU $6                  ; AUXADRPOINTER
R3H             EQU $7                  ;
R4L             EQU $8                  ; AUXADRPOINTER.
R4H             EQU $9                  ;
R5L             EQU $A                  ; AUXADRPOINTER.
R5H             EQU $B                  ;
R6L             EQU $C                  ; GALLOPBITMASK.
R6H             EQU $D                  ; ($0001 TO 2^N)
YSAV            EQU $34                 ; MONITORSCANINDEX.
A1H             EQU $3D                 ; BEGINTESTBLOCKADR.
A2L             EQU $3E                 ; LEN(PAGES)FROMMON.
SETCTLY         EQU $D5B0               ; SETUPCNTRL-YLOCATION
PRBYTE          EQU $FDDA               ; BYTEPRINTSUBR.
COUT            EQU $FDED               ; CHAROUT SUBR
PRERR           EQU $FF2D               ; PRINTS 'ERR-BELL'
BEL             EQU $FF3A               ;
                                        ;
;RAMTEST                                ;
                ORG $D5BC               ;
SETUP           LDA #$C3                ; SETUPCNTRL-VLOCATION
                LDY #$D5                ;
                JMP SETCTLY             ;
RAMTST          LDA #$0                 ; TESTFOR$0.
                JSR TEST                ;
                LDA #$F                 ; THEN$F.
                JSR TEST                ;
                JMP BEL                 ;
TEST            STA TDATA               ;
                EOR #$FF                ;
                STA NDATA               ;
                LDA A1H                 ;
                STA R3H                 ; INIT(R3L,R3H)
                STA R4H                 ; (R4L,R4H),(R5L,R5H)
                STA R5H                 ; TOTESTBLOCKBEGIN
                LDY #$0                 ; ADDRES.
                STY R3L                 ;
                STY R4L                 ;
                STY R5L                 ;
                LDX A2L                 ; LENGTH(PAGES).
                LDA TDATA               ;
TEST01          STA (R4L),Y             ; SETENTIRETEST
                INY                     ; BLOCKTODATA.
                BNE TEST01              ;
                INC R4H                 ;
                DEX                     ;
                BNE TEST01              ;
                LDX A2L                 ;
TEST02          LDA (R3L),Y             ; VERIFYENTIRE
                CMP TDATA               ; TESTBLOCK.
                BEQ TEST03              ;
                PHA                     ; PRESERVEBADDATA.
                LDA R3H                 ;
                JSR PRBYTE              ; PRINTADDRES,
                TYA                     ;
                JSR PRBYSP              ;
                LDA TDATA               ; THENEXPECTEDDATA,
                JSR PRBYSP              ;
                PLA                     ; THENBADDATA,
                JSR PRBYCR              ; THEN'ERR-BELL'.
TEST03          INY                     ;
                BNE TEST02              ;
                INC R3H                 ;
                DEX                     ;
                BNE TEST02              ;
                LDX A2L                 ; LENGTH.
TEST04          LDA NDATA               ;
                STA (R5L),Y             ; SETTESTCELTO
                STY R6H                 ; NDATAANDR6
                STY R6L                 ; (GALLOPBITMASK)
                INC R6L                 ; TO$01.
TEST05          LDA NDATA               ;
                JSR TEST6               ; GALLOPWITHNDATA
                LDA TDATA               ;
                JSR TEST6               ; THENWITHDATA.
                ASL R6L                 ;
                ROL R6H                 ; SHIFTGALOPBIT
                LDA R6H                 ; MASKFORNEXT
                CMP A2L                 ; NEIGHBOR.DONE
                BCC TEST05              ; IF>LENGTH.
                LDA TDATA               ;
                STA (R5L),Y             ; RESTORETESTCEL.
                INC R5L                 ;
                BNE TEST04              ;
                INC R5H                 ; INCRTESTCEL
                DEX                     ; POINTERANDDECR
                BNE TEST04              ; LENGTHCOUNT.
RTSI            RTS                     ;
TEST6           STA TESTD               ; SAVEGALLOPDATA.
                LDA R5L                 ;
                EOR R6L                 ; SETR4TOR5
                STA R4L                 ; EX-ORR6
                LDA R5H                 ; FORNEIGHBOR
                EOR R6H                 ; ADDRES(1BIT
                STA R4H                 ; DIFERENCE)
                LDA TESTD               ;
                STA (R4L),Y             ; GALOPTEST.DATA.
                LDA (R5L),Y             ; CHECKTESTCEL
                CMP NDATA               ; FORCHANGE.
                BEQ RTSI                ; (OK).
                PHA                     ; PRESERVEFAILDATA.
                LDA R5H                 ;
                JSR PRBYTE              ; PRINTTESTCELL
                LDA R5L                 ; ADDRES,
                JSR PRBYSP              ;
                LDA NDATA               ;
                STA (R5L),Y             ; (REPLACECORRECTDATA)
                JSR PRBYSP              ; THENTESTDATABYTE.
                PLA                     ;
                JSR PRBYSP              ; THENFAILDATA,
                LDA R4H                 ;
                JSR PRBYTE              ;
                LDA R4L                 ; THENNEIGHBORADR,
                JSR PRBYSP              ;
                LDA TESTD               ; THENGALLOPDATA.
PRBYCR          JSR PRBYSP              ; OUTPUTBYTE,SPACE.
                JSR PRERR               ; THEN 'ERR-BELL'.
                LDA #$8D                ; ASCII CAR. RETURN.
                JMP COUT                ;
PRBYSP          JSR PRBYTE              ;
                LDA #$A0                ; OUTPUT BYTE. THEN
                JMP COUT                ; SPACE.
                ORG $3F8                ;
USRLOC          JMP RAMTST              ; ENTRYPROMMON(CTRL-Y)
