                .FEATURE LABELS_WITHOUT_COLONS
                .EXPORT DISK2



SECYNIBS        =     $56
BOOT1PAGE       =     $08



BUFPTR          =     $26
SLOT            =     $2B
TMP             =     $3C
SECTOR          =     $3D
A3L             =     $40
TRACK           =     $41

STACKBASE       =     $0100
NIBBUF          =     $0300
XLATE           =     NIBBUF+SECYNIBS
BOOT2BASE       =     BOOT1PAGE << 8

SWITCHBASE      =     $C080
STEPOFFBASE     =     SWITCHBASE+$0
STEPONBASE      =     STEPOFFBASE+$1
MOTORON         =     SWITCHBASE+$9
DRV0EN          =     SWITCHBASE+$A
Q6OFF           =     SWITCHBASE+$C
Q7OFF           =     SWITCHBASE+$E

WAIT            =     $FCA8
MONRTS          =     $FF58



                                ; build translation table
DISK2           LDX   #$20
                LDY   #0
                LDX   #$03
LC606           STX   TMP
                TXA
                ASL   A
                BIT   TMP
                BEQ   LC61E
                ORA   TMP
                EOR   #%11111111
                AND   #%01111110
LC614           BCS   LC61E
                LSR   A
                BNE   LC614
                TYA
                STA   XLATE,X
                INY
LC61E           INX
                BPL   LC606





                JSR   MONRTS    ; calculate slot based on ROM code address
                TSX
                LDA   STACKBASE,X
                ASL   A
                ASL   A
                ASL   A
                ASL   A
                STA   SLOT



                TAX
                LDA   Q7OFF,X   ; set drive to "read" mode
                LDA   Q6OFF,X
                LDA   DRV0EN,X  ; engage drive 0
                LDA   MOTORON,X ; turn drive motor on





                LDY   #$50      ; move arm to track 0
LC63D           LDA   STEPOFFBASE,X
                TYA
                AND   #%00000011
                ASL   A
                ORA   SLOT
                TAX
                LDA   STEPONBASE,X
                LDA   #SECYNIBS
                .IFDEF NODELAY
                LDA   #0
                NOP
                .ELSE
                JSR   WAIT
                .ENDIF
                DEY
                BPL   LC63D




                                ; set params for READSECT
                STA   BUFPTR
                STA   SECTOR    ; SECTOR 0
                STA   TRACK     ; TRACK 0
                LDA   #BOOT1PAGE
                STA   BUFPTR+1  ; buffer $800


                                ; read SECTOR to (BUFPTR) buffer
READSECT        CLC
READSECT2       PHP

                                ; look for D5 AA 96 (address) or D5 AA AD (data)
LC65E           LDA   Q6OFF,X
                BPL   LC65E
LC663           EOR   #$D5
                BNE   LC65E
LC667           LDA   Q6OFF,X
                BPL   LC667
                CMP   #$AA
                BNE   LC663
                NOP
LC671           LDA   Q6OFF,X
                BPL   LC671
                CMP   #$96
                BEQ   ADDRHANDLER
                PLP
                BCC   READSECT
                EOR   #$AD
                BEQ   DATAHANDLER
                BNE   READSECT




                                ; get T/S number from address header
ADDRHANDLER     LDY   #3
LC685           STA   A3L
LC687           LDA   Q6OFF,X
                BPL   LC687
                ROL   A
                STA   TMP
LC68F           LDA   Q6OFF,X
                BPL   LC68F
                AND   TMP
                DEY
                BNE   LC685
                PLP
                CMP   SECTOR
                BNE   READSECT  ; branch if not the sector we're looking for
                LDA   A3L
                CMP   TRACK
                BNE   READSECT  ; branch if not the track we're looking for
                BCS   READSECT2





DATAHANDLER     LDY   #SECYNIBS ; read $56 nibblized bytes to $300
LC6A8           STY   TMP
LC6AA           LDY   Q6OFF,X
                BPL   LC6AA
                EOR   XLATE-$80,Y
                LDY   TMP
                DEY
                STA   NIBBUF,Y
                BNE   LC6A8

LC6BA           STY   TMP       ; read $100 nibblized bytes to (BUFPTR)
LC6BC           LDY   Q6OFF,X
                BPL   LC6BC
                EOR   XLATE-$80,Y
                LDY   TMP
                STA   (BUFPTR),Y
                INY
                BNE   LC6BA

LC6CB           LDY   Q6OFF,X
                BPL   LC6CB
                EOR   XLATE-$80,Y
LC6D3           BNE   READSECT  ; branch if checksum is wrong



                                ; denibblize into (BUFPTR)
                LDY   #0
LC6D7           LDX   #SECYNIBS

LC6D9           DEX
                BMI   LC6D7
                LDA   (BUFPTR),Y
                LSR   NIBBUF,X
                ROL   A
                LSR   NIBBUF,X
                ROL   A
                STA   (BUFPTR),Y
                INY
                BNE   LC6D9
                INC   BUFPTR+1
                INC   SECTOR
                LDA   SECTOR
                CMP   BOOT2BASE
                LDX   SLOT
                BCC   LC6D3
                JMP   BOOT2BASE+1
