         .FEATURE LABELS_WITHOUT_COLONS
         .INCLUDE "SYMBOLS.A65"
;-----------------------------------------------------------------------
;
; MATH ROUTINES
;
; 16-bit multiplication and division functions (not used anywhere).
;
;-----------------------------------------------------------------------





MULPM    JSR   MD1        ;ABS VAL OF AC AUX
MUL      LDY   #$10       ;INDEX FOR 16 BITS
MUL2     LDA   ACL        ;ACX * AUX + XTND
         LSR   A          ; TO AC, XTND
         BCC   MUL4       ;IF NO CARRY,
         CLC              ; NO PARTIAL PROD.
         LDX   #$FE
MUL3     LDA   XTNDL+2,X  ;ADD MPLCND (AUX)
         ADC   AUXL+2,X   ; TO PARTIAL PROD
         STA   XTNDL+2,X  ; (XTND)
         INX
         BNE   MUL3
MUL4     LDX   #$03
MUL5     ROR   ACL,X
         DEX
         BPL   MUL5
         DEY
         BNE   MUL2
         RTS



DIVPM    JSR   MD1        ;ABS VAL OF AC, AUX.
DIV      LDY   #$10       ;INDEX FOR 16 BITS
DIV2     ASL   ACL
         ROL   ACH
         ROL   XTNDL      ;XTND/AUX
         ROL   XTNDH      ;  TO AC.
         SEC
         LDA   XTNDL
         SBC   AUXL       ;MOD TO XTND.
         TAX
         LDA   XTNDH
         SBC   AUXH
         BCC   DIV3
         STX   XTNDL
         STA   XTNDH
         INC   ACL
DIV3     DEY
         BNE   DIV2
         RTS



MD1      LDY   #$00       ;ABS VAL OF AC, AUX
         STY   SIGN       ;  WITH RESULT SIGN
         LDX   #AUXL      ;  IN LSB OF SIGN.
         JSR   MD3
         LDX   #ACL
MD3      LDA   LOC1,X     ;X SPECIFIES AC OR AUX
         BPL   MDRTS
         SEC
         TYA
         SBC   LOC0,X     ;COMPL SPECIFIED REG
         STA   LOC0,X     ;  IF NEG.
         TYA
         SBC   LOC1,X
         STA   LOC1,X
         INC   SIGN
MDRTS    RTS
